*** Settings ***

Resource  plone.txt

Suite Setup  Setup

*** Variables ***

*** Test cases ***

Test Add User 1
    Goto Homepage
    Add User  someuser  Some User  someuser@email.com  password

Test Add User 2
    Goto Homepage
    Add User  someuser2  Some User2  someuser2@email.com  password

Test Add Task Folder
    Goto Homepage
    Add Panel Tasks  Tasks  someuser  someuser2
    Open Menu  plone-contentmenu-workflow
    Click Link  css=a#workflow-transition-publish
    Page Should Contain  Item state changed

Test Add First Task
    Goto Homepage
    Log Out
    Log In  someuser  password
    Click Link  Tasks
    Add Task  First Task  someuser2  Alta

Test Add Second Task
    Goto Homepage
    Log Out
    Log In  someuser2  password
    Click Link  Tasks
    Add Task  Second Task  someuser  Baixa

Test Add Response Fist Task
    Goto Homepage
    Log Out
    Log In  someuser  password
    Click Link  Tasks
    Click Link  First Task
    Add Response  assign  someuser2  Alta

Test Add Response Second Task
    Goto Homepage
    Log Out
    Log In  someuser2  password
    Click Link  Tasks
    Click Link  Second Task
    Add Response  assign  someuser  Baixa

*** Keywords ***

Setup
    Log In  admin  admin

Add User
    [arguments]  ${user_name}  ${full_name}  ${email}  ${password}

    Go to  ${PLONE_URL}/@@new-user
    Page should contain element  form.username
    Input Text  form.fullname  ${full_name}
    Input Text  form.username  ${user_name}
    Input Text  form.email  ${email}
    Input Password  form.password  ${password}
    Input Password  form.password_ctl  ${password}
    Click Button  Register
    Page Should Contain  ${full_name}

Add Panel Tasks
    [arguments]  ${title}  ${responsible}  ${can_add_tasks}

    Open Add New Menu
    Click Link  css=a#s17.taskmanager-taskfolder
    Page Should Contain  Add Panel tasks
    Input Text  css=input#form-widgets-IBasic-title  ${title}
    Select From List  xpath=//select[@name="form.widgets.responsible:list"]  ${responsible}
    Select From List  xpath=//select[@name="form.widgets.can_add_tasks.from"]  ${can_add_tasks}
    Click Button  xpath=//button[@name="from2toButton"]
    Click Button  Save
    Page Should Contain  Item created

Add Task
    [arguments]  ${title}  ${responsible}  ${priority}

    Open Add New Menu
    Click Link  css=a#s17.taskmanager-task
    Page Should Contain  Add Task
    Input Text  css=input#form-widgets-title  ${title}
    Select From List  xpath=//select[@name="form.widgets.responsible:list"]  ${responsible}
    Select From List  xpath=//select[@name="form.widgets.priority:list"]  ${priority}
    Click Button  Save
    Page Should Contain  Item created

Add Response
    [arguments]  ${state}  ${responsible}  ${priority}

    Select From List  xpath=//select[@name="transition"]  ${state}
    Select From List  xpath=//select[@name="responsible"]  ${responsible}
    Select From List  xpath=//select[@name="priority"]  ${priority}
    Click Button  Submit
    Page Should Contain  Added by



